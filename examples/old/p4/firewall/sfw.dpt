/* A stateful firewall with a cuckoo insertion thread. */
/* 
should compile to ~9 stages on tofino 
to reduce the number of stages, we can: 
1. inline hash calculation into array access (this may save 2 stages)
2. use more clever salu input register allocation algorithm (this may save 1-2 stages)

*/
const int num_entries = 1024;
const int timeout_thresh = 10000;
const int NULL = 0;
const int TRUE = 1;
const int FALSE = 0;

event packetin (int<<32>> src, int<<32>> dst);
event continue (int<<32>> src, int<<32>> dst);
event insert (int src, int dst, int ts, int first_src, int first_dst, int<<8>> retry_ct);
event reinsert(int src, int dst, int ts, int first_src, int first_dst, int<<8>> retry_ct);
event scan (int prev_idx, int<<8>> prev_stage);
event delete (int cur_idx, int<<8>> cur_stage);

global v_1_src = new Array<<32>>(num_entries);
global v_1_dst = new Array<<32>>(num_entries);

global v_1_ts  = new Array<<32>>(num_entries);

global v_2_src = new Array<<32>>(num_entries);
global v_2_dst = new Array<<32>>(num_entries);

global v_2_ts  = new Array<<32>>(num_entries);

global src_stash = new Array<<32>>(1);
global dst_stash = new Array<<32>>(1);

/* get_functions */
memop get_mem(int stored_val, int unused) {
    return stored_val;
}
memop get_cmp(int stored_val, int target_val) {
    if (stored_val == target_val){
        return TRUE;
    }
    else {
        return FALSE;
    }
}
/* check if timestamp is older than threshold. */
memop get_is_over_thresh(int stored_ts, int cur_ts) {
    if (cur_ts - stored_ts > timeout_thresh) {
        return TRUE;
    }
    else {
        return FALSE;
    }
}
/* set functions */
memop set_mem(int stored_val, int new_val) { 
    return new_val;
}
memop set_mem_if_null(int stored_val, int new_val) {
    if (stored_val == NULL) {
        return new_val;
    }
    else {
        return stored_val;
    }
}

handle packetin (int<<32>> src, int<<32>> dst) {
    int<<32>> h1h = hash<<32>>(10398247, src, dst); 
    int cur_ts = Sys.time(1);
    
    /* check if the key is in the first hash table stage */
    int v_1_src_stored = Array.getm(v_1_src, h1h, get_cmp, src);
    int v_1_dst_stored = Array.getm(v_1_dst, h1h, get_cmp, dst);
    /* found in first stage of hash table */
    if (v_1_src_stored==1 && v_1_dst_stored==1) {
        Array.setm(v_1_ts, h1h, set_mem, cur_ts);
        generate continue(src, dst);
    }
    else {
        int h2h = hash<<32>>( 1295981879, src, dst);
        /* check if the key is in the second hash table stage. */
        int v_2_src_stored = Array.getm(v_2_src, h2h, get_cmp, src);
        int v_2_dst_stored = Array.getm(v_2_dst, h2h, get_cmp, dst);
        if (v_2_src_stored==1 && v_2_dst_stored==1) {
            // set last access timestamp of this flow to cur_ts
            Array.setm(v_2_ts, h2h, set_mem, cur_ts);
            generate continue(src, dst);
        }
        /* not found in either table */
        else {
            /* check stash for forward direction of flow */
            if (src == 10) {
                // fetch contents of stash, write flow key to the stash if its empty. 
                int stashed_src = Array.update(src_stash, 0, get_mem, 0, set_mem_if_null, src);
                int stashed_dst = Array.update(dst_stash, 0, get_mem, 0, set_mem_if_null, dst);

                int stashed_src_eq = src - stashed_src;
                int stashed_dst_eq = dst - stashed_dst;
                /* stash is empty: spawn an insert event and allow the packet to continue. */
                if (stashed_src == 0 && stashed_dst == 0) {
                    generate insert (src, dst, cur_ts, src, dst, 0);
                    generate continue (src, dst);
                }
                /* stash is not empty. 
                We only continue if current flow is pending an install.  */
                else {
                    if (stashed_src_eq == 0 && stashed_dst_eq == 0) {
                        generate continue (src, dst);                    
                    }
                }
            }
            /* return flow stash check */
            else {
                /* check if reverse direction of the flow is in the stash. */
                int src_stored_stash = Array.getm(src_stash, 0, get_cmp, dst);
                int dst_stored_stash = Array.getm(dst_stash, 0, get_cmp, src);
                if (src_stored_stash == 0 && dst_stored_stash == 0) {
                    generate continue(src, dst);
                }
            }
        }
    }
}

handle insert (int src, int dst, int ts, int first_src, int first_dst, int<<8>> retry_ct) {
    int h1h = hash<<32>>( 10398247, src, dst);
    /* insert and evict to stage 1 */
    int src_victim = Array.update(v_1_src, h1h, get_mem, 0, set_mem, src);
    int dst_victim = Array.update(v_1_dst, h1h, get_mem, 0, set_mem, dst);
    int ts_victim = Array.update(v_1_ts, h1h, get_mem, 0, set_mem, ts);

    /* if there was a collision, insert and evict to stage 2 */
    if (src_victim == 0 && dst_victim == 0) { 
        int j1 = 0;
    }
    else {
    // if (src_victim != 0 || dst_victim != 0) { 
        /* insert and evict to stage 2. */
        int h2h = hash<<32>>( 1295981879, src_victim, dst_victim);
        int v_2_src_victim = Array.update(v_2_src, h2h, get_mem, 0, set_mem, src_victim);
        int v_2_dst_victim = Array.update(v_2_dst, h2h, get_mem, 0, set_mem, dst_victim);
        int v_2_ts_victim = Array.update(v_2_ts, h2h, get_mem, 0, set_mem, ts_victim);

        if (v_2_src_victim == 0 && v_2_dst_victim == 0) { 
            int j2 = 0;
        }
        else {
        // if (v_2_src_victim != 0 || v_2_dst_victim != 0) { 
            /* we've evicted something from the second stage. 
            This entry needs to be re-inserted, so put it in the stash. */
            Array.setm(src_stash, 0, set_mem, v_2_src_victim);
            Array.setm(dst_stash, 0, set_mem, v_2_dst_victim);
            generate reinsert(v_2_src_victim, v_2_dst_victim, v_2_ts_victim, first_src, first_dst, retry_ct);
        }
    }
}
/* check for an insertion cycle and possibly call insert again. */
handle reinsert(int src, int dst, int ts, int first_src, int first_dst, int<<8>> retry_ct) {
    /* check if we're re-inserting the original element */
    int src_diff = src - first_src;
    int dst_diff = dst - first_dst;    
    if (src_diff == 0 && dst_diff == 0) {
        retry_ct = retry_ct + 1;
    }
    /* A retry_ct of 2 means that there is an unresolvable cycle 
    (we consider the table full.) */
    if (retry_ct == 2) {        
        Array.set(src_stash, 0, 0);
        Array.set(dst_stash, 0, 0); 
    }
    /* if we did not reach a cycle, insert again. */
    else {  
        generate insert(src, dst, ts, first_src, first_dst, retry_ct);
    }
}

handle scan (int prev_idx, int<<8>> prev_stage) {
    /* calculate address to scan */
    int ts = Sys.time(1);
    int over_thresh = 0; 
    int next_scan_idx = 0;
    int<<8>> next_stage = 0;
    if (prev_stage == 1) {
        next_stage = 2;
    }
    else { /* prev_stage == 2 */
        next_scan_idx = prev_idx + 1;
        next_stage = 1;
    }
    if (next_stage == 1) {
        // Check if an entry in stage 1 of the map is over threshold. 
        over_thresh = Array.getm(v_1_ts, next_scan_idx, get_is_over_thresh, ts);
    }
    else {
        // Check if an entry in stage 2 of the map is over threshold. 
        over_thresh = Array.getm(v_2_ts, next_scan_idx, get_is_over_thresh, ts);    
    }
    if (over_thresh == 1) {
        // delete this index. 
        generate delete(next_scan_idx, next_stage);
    }
    else {
        // continue scanning. 
        generate scan(next_scan_idx, next_stage);            
    }
}

handle delete (int cur_idx, int<<8>> cur_stage) {
    /* set all arrays at this position to zero.  */
    if (cur_stage == 1) {
        Array.set(v_1_src, cur_idx, 0);
        Array.set(v_1_dst, cur_idx, 0);
        Array.set(v_1_ts, cur_idx, 0);
    }
    else {
        Array.set(v_2_src, cur_idx, 0);
        Array.set(v_2_dst, cur_idx, 0);
        Array.set(v_2_ts, cur_idx, 0);
    }
    /* continue scanning */
    generate scan(cur_idx, cur_stage);    
}
