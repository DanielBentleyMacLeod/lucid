/* A stateful firewall with two stages 
   to mitigate collisions. */

const int seed_1 = 7;
const int seed_2 = 13;
const int NULL = 0;
const int TRUE = 1; 
const int FALSE = 0;
const int<<8>> TOS_TRUSTED = 1;

entry event ip_in (int<<9>> igr_port, int src, int dst, int<<16>> len, int<<8>> tos);
exit event ip_out (int<<1>> drop, int<<9>> egr_port);

global Array.t<<32>> key_src_1 = Array.create(1024);
global Array.t<<32>> key_dst_1 = Array.create(1024);
global Array.t<<32>> key_src_2 = Array.create(1024);
global Array.t<<32>> key_dst_2 = Array.create(1024);

memop incr(int memval, int incrval) {
    return memval + incrval;
}
memop always_mem(int memval, int newval) {
    return memval;
}
memop mem_eq_search(int memval, int searchval){
    if (memval == searchval){
        return TRUE;
    } else {
        return FALSE;
    }
} 
memop new_if_null(int memval, int newval){
    if (memval == NULL){
        return newval;
    } else {
        return memval;
    }
}
// if memval is null or searchval, return null.
// otherwise, return memval. 
// memval == null -> null
// memval == searchval --> null
// (memval != null) && (memval != searchval) --> memval
memop null_if_eq(int memval, int searchval){
    if (memval == searchval) {
        return NULL;
    } else {
        return memval;
    }
}

handle ip_in (int<<9>> igr_port, int src, int dst, int<<16>> len, int<<8>> tos) {    
    // case: flow from trusted source.
    if (tos == TOS_TRUSTED) {
        int<<10>> idx_1 = hash<<10>>(seed_1, src, dst);
        // put this flow's key in the table if the slot is empty, return the previous value. 
        int stg_1_src = Array.update(key_src_1, idx_1, null_if_eq, src, new_if_null, src);
        int stg_1_dst = Array.update(key_dst_1, idx_1, null_if_eq, dst, new_if_null, dst);
        // if the stage 1 record was null or the flow's key, this flow is stored 
        // and we can safely emit the packet. 
        if ((stg_1_src == NULL) && (stg_1_dst == NULL)) {
            generate ip_out(0, igr_port);
        // else, repeat for stage 2.
        } else {
            int<<10>> idx_2 = hash<<10>>(seed_2, src, dst);
            int stg_2_src = Array.update(key_src_2, idx_2, null_if_eq, src, new_if_null, src);
            int stg_2_dst = Array.update(key_dst_2, idx_2, null_if_eq, dst, new_if_null, dst);
            if ((stg_2_src == NULL) && (stg_2_dst == NULL)) {
                generate ip_out(0, igr_port);
            }
        }
    // case: flow from untrusted source. This is only allowed 
    // if the reverse direction flow has been seen. 
    } else {
        int<<10>> idx_1 = hash<<10>>(seed_1, dst, src);
        int src_match_1 = Array.update(key_src_1, idx_1, mem_eq_search, dst, always_mem, 0);
        int dst_match_1 = Array.update(key_dst_1, idx_1, mem_eq_search, src, always_mem, NULL);
        int<<10>> idx_2 = hash<<10>>(seed_2, dst, src);
        int src_match_2 = Array.update(key_src_2, idx_2, mem_eq_search, dst, always_mem, 0);
        int dst_match_2 = Array.update(key_dst_2, idx_2, mem_eq_search, src, always_mem, NULL);
        // if the flow is found in either stage, the packet is permitted to exit.
        if (((src_match_1 == TRUE) && (dst_match_1 == TRUE)) || 
            ((src_match_2 == TRUE) && (dst_match_2 == TRUE))) {
            generate ip_out(0, igr_port);
        }
    }
}